***************************************************************************
* Put here the custom event handler
***************************************************************************

*---------------------------------------------------------------------------------	
PROCEDURE E_Doc2AfterBuildCrmFolderGroup 
	PARAMETERS poDocForm

	&& called after set CRM FOLDER GROUP in DOCFORM

	poDocForm.zBeginGroup( "w_L:" )
	
		poDocForm.zBeginGroup( "BW9:" )

			poDocForm.zAddLabel(":", "Gowam Peppol :" , "")
			=ADDPROPERTY(poDocForm, "zoC_PeppolTextBoxStatus", poDocForm.zAddTextBox("D:", "C_GetPeppolTecStatus(c_dochead.jnl, c_dochead.number)", 70, "", ".T.", ".T.", 0, 0, ""))
			
			=ADDPROPERTY(poDocForm, "zoC_PeppolEditBox", poDocForm.zAddEditBox( "", "C_GetPeppolTecStatus(c_dochead.jnl, c_dochead.number,'desc')", 70, 4, "", ".T.", ".T."))
			poDocForm.zoC_PeppolEditBox.ReadOnly = .T.
			=ADDPROPERTY(poDocForm, "zoC_RefreshPeppolTimer", CREATEOBJECT("C_RefreshPeppolTimer", poDocForm))
			
		poDocForm.zEndGroup( "" )	

	poDocForm.zEndGroup( "" )
		
*--------------------------------------------------------------------------------
FUNCTION C_GetPeppolTecStatus
	LPARAMETERS pcJnl, pnNumber, pcType
	
	LOCAL lnOldSelect, lcReturn, llCheckFullSql, lnVersion
	
	lnOldSelect 	= SELECT(0)
	lcReturn		= ""
	llCheckFullSql	= .T.
	
	pcJnl = PADR(pcJnl,8)
	
	IF NOT goCache.DocHeadExist(pcJnl, pnNumber)
		RETURN lcReturn
	ENDIF
	
	lnVersion = VAL(AppVersion())
	
	IF lnVersion < 7.0
		llCheckFullSql = .F.
	ENDIF
	
	IF llCheckFullSql
		IF isFullSql()
			FlexDB_USE("hist")	
		ELSE
			IF !USED("hist")
				=OpenTable("Hist", "LnkDoc", "Hist")
			ENDIF
		ENDIF
	ELSE
		IF !USED("hist")
			=OpenTable("Hist", "LnkDoc", "Hist")
		ENDIF
	ENDIF	
	
	IF EMPTY(pcType)
		pcType = ""
	ENDIF
		
	SELECT hist
	SET ORDER TO TAG LnkDoc
	SET KEY TO pcJnl+STR(pnNumber,8)
	SET FILTER TO hist.histtype = "PEPPOL"
	LOCATE FOR hist.histstatut = "OK "
	IF !FOUND()
		GO TOP
		GO BOTTOM
	ENDIF
	IF EOF()
		IF !EMPTY(pcType)
			lcReturn = MSG("No Peppol historic for this document %1 %2", pcJnl, pnNumber)
		ENDIF
		SELECT (lnOldSelect)
		RETURN lcReturn
	ENDIF
	
	IF EMPTY(pcType)
		lcReturn = "Status : "+ALLTRIM(hist.histstatut)+ " - "+ALLTRIM(hist.subject)
	ELSE
		IF hist.histstatut = "KO"
			lcReturn = ALLTRIM(STRCONV(hist.bbody,11))
		ENDIF
	ENDIF
	
	SELECT (lnOldSelect)
	
	RETURN lcReturn

*--------------------------------------------------------------------------------
FUNCTION C_AddHistPeppolAction
	LPARAMETERS pcJnl, pnNumber, pcHistStatus, pcSubject
	
	LOCAL lnOldSelect, lcReturn, lcThirdId
	
	lnOldSelect = SELECT(0)
	lcReturn	= ""
	
	IF NOT goCache.DocHeadExist(pcJnl, pnNumber)
		RETURN lcReturn
	ENDIF
	
	lcThirdId = goCache.DocHeadGetFieldValue(pcJnl, pnNumber, "THIRDID", "")
	
	IF !USED("hist")
		=OpenTable("Hist", "LnkDoc", "Hist")
	ENDIF
	
	SELECT hist
	
	APPEND BLANK
	REPLACE Hist.HistId 		WITH GetNewId()
	REPLACE Hist.HistType 		WITH "PEPPOL"
	REPLACE Hist.HistStatut 	WITH pcHistStatus
	REPLACE Hist.Subject 		WITH pcSubject
	REPLACE Hist.RefJnl 		WITH pcJnl
	REPLACE Hist.RefNumber		WITH pnNumber
	REPLACE Hist.Date			WITH DATE()
	REPLACE Hist.ParentTbl		WITH "CUST"
	REPLACE Hist.ParentId		WITH lcThirdId
	REPLACE Hist.Time			WITH TTOC(DATETIME(), 2)
	DO FlexDb_ModDate
	
	SELECT (lnOldSelect)
	
	RETURN 	
	
*--------------------------------------------------------------------------------
PROCEDURE E_Doc2MenuBuild
	PARAMETERS pcJnl, pnNumber, pcContext, plCalledFromList, poCallingForm, poCallingMenu

*	Old version but compatible version 6.0
*	DEFINE BAR 1001 OF DocNewOtherOption PROMPT "My test"
*	ON SELECTION BAR 1001 OF DocNewOtherOption DO MyTest

*	From version 6.0
*	poCallingMenu.DefineBar( pcPicture, pcToolTipText, pcClickProc, plUseMainTable, pcCaption, plNoOption, pcCmd )
*	Example : poCallingMenu.DefineBar( PicExcel(), l("4957^Exporter vers Excel"), "_Screen.ActiveForm.zoFormMainGroup.DocdetExportExcel()" )	

	poCallingMenu.DefineBar( PicExport(), "Send to Peppol through Gowam", "C_SendToPeppolViaGowam()")

*--------------------------------------------------------------------------------
PROCEDURE E_Doc2CanMod
	PARAMETERS pcJnl, pnNumber, plCanMod, poDocForm
	
	&& By default : plCanMod =.T.

	LOCAL lnOldSelect, lcJnlType, lcStatus
	
	lnOldSelect 	= SELECT(0)
	lcJnlType 		= goCache.JnlGetFieldValue(pcJnl, "TYPE", "")
	lcStatus		= ""
	
	IF lcJnlType = "CI" OR lcJnlType = "CC"
		lcStatus = C_GetPeppolTecStatus(pcJnl, pnNumber)	
		IF "OK" $ lcStatus AND NOT "not to sent" $ LOWER(lcStatus)
			SELECT (lnOldSelect)
			Stop("You cannot modify this document, it has been successfully sent over the Peppol network.")
			plCanMod = .F.
			RETURN plCanMod
		ENDIF
		IF "WAIT" $ lcStatus 
			IF NOT YesNoConfirm("This document is currently being processed by Gowam, do you really want to modify it ? Click Yes if you are certain that Gowam is not processing it")
				SELECT (lnOldSelect)
				plCanMod = .F.
				RETURN plCanMod			
			ENDIF
		ENDIF					
	ENDIF
	
	SELECT (lnOldSelect)
 	
	RETURN plCanMod	
	
*--------------------------------------------------------------------------------
FUNCTION E_Doc2CanDel
	PARAMETERS pcJnl, pnNumber, poCallingForm
	
	LOCAL lnOldSelect, lcJnlType, lcStatus
	
	lnOldSelect 	= SELECT(0)
	lcJnlType 		= goCache.JnlGetFieldValue(pcJnl, "TYPE", "")
	lcStatus		= ""
	
	IF lcJnlType = "CI" OR lcJnlType = "CC"
		lcStatus = C_GetPeppolTecStatus(pcJnl, pnNumber)	
		IF "OK" $ lcStatus AND NOT "not to sent" $ LOWER(lcStatus)
			Stop("You cannot delete this document, it has been successfully sent over the Peppol network.")
			SELECT (lnOldSelect)
			RETURN .F.
		ENDIF
		IF "WAIT" $ lcStatus 
			IF NOT YesNoConfirm("This document is currently being processed by Gowam, do you really want to delete it ?")
				SELECT (lnOldSelect)
				RETURN .F.
			ENDIF
		ENDIF					
	ENDIF
	
	SELECT (lnOldSelect)
 	
 	RETURN .T. 
 	
*--------------------------------------------------------------------------------	
PROCEDURE E_DocAfterPrDoc
	PARAMETERS pcJnl, pnNum1, pnNum2, plDirectprint, pnAction
	
	IF TYPE("pcSoc") = "C"
		&& Link to accounting in progress, and send PDF to VI in progress
		C_SendToPeppolLog( "E_DocAfterPrDoc call cancelled - Link to accounting in progress, and send PDF to VI in progress" )
		RETURN
	ENDIF
	
	&& Called after print all document.
	&& pnAction : 1 = page setup, 2 = print preview, 3 = print, ...
	
	IF (goCache.JnlGetFieldValue(pcJnl, "TYPE", "") = "CI" OR goCache.JnlGetFieldValue(pcJnl, "TYPE", "") = "CC") AND INLIST(pnAction, 3, 4, 5, 6)
		LOCAL lcMessage, lcStatus
		lcMessage = ""
		lcStatus  = ""
		IF pnNum1 = pnNum2
			lcMessage = MSG("Do you want to send this document %1 %2 to Peppol through Gowam ?", pcJnl, pnNum1)
			&& Send one document
			lcStatus = C_GetPeppolTecStatus(pcJnl, pnNum1)
			IF "OK" $ lcStatus AND NOT "not to sent" $ LOWER(lcStatus)
				&& Already sent, nothing to do
				lcMessage = ""
			ELSE
				IF YesNo(lcMessage)
					lcMessage = ""
					C_SendToPeppol(pcJnl, TRANSFORM(pnNum1), TRANSFORM(pnNum2), @lcMessage)	
				ELSE
					lcMessage = ""
				ENDIF
			ENDIF
		ELSE
			lcMessage = MSG("Do you want to send this range of documents (journal %1 from %2 to %3) to Peppol through Gowam ?", pcJnl, pnNum1, pnNum2)
			IF YesNo(lcMessage)
				lcMessage = ""
				C_SendToPeppol(pcJnl, TRANSFORM(pnNum1), TRANSFORM(pnNum2), @lcMessage)
			ELSE
				lcMessage = ""
			ENDIF
		ENDIF
		IF !EMPTY(lcMessage)
			C_SendToPeppolLog( MSG("E_DocAfterPrDoc called and result error : pcJnl = %1, pnNum1 = %2, pnNum2 = %3", pcJnl, pnNum1, pnNum2) )
			Stop(lcMessage)
		ENDIF
	ENDIF
	
	RETURN

*--------------------------------------------------------------------------------	
PROCEDURE E_DocPrintTrapEmail
	PARAMETERS pcJnl, pnNum, pnLayout, pcEmail
	
	LOCAL lcThirdId, lcJnlType, lcEmailAdressForInvoice, lcEmailAdressForOtherDoc
	
	lcJnlType = goCache.JnlGetFieldValue(pcJnl, "TYPE", "")
	
	lcThirdId = goCache.DocHeadGetFieldValue(pcJnl, pnNum, "THIRDID", "")

	IF EMPTY(lcThirdId)
		RETURN .F.
	ENDIF

	lcEmailAdressForInvoice 	= LOWER(ALLTRIM(goCache.CustGetFieldValue(lcThirdId, "EMAIL", "")))
	lcEmailAdressForOtherDoc 	= LOWER(ALLTRIM(goCache.CustGetFieldValue(lcThirdId, "C_EMAILOD", "@#@")))
	
	IF lcEmailAdressForOtherDoc = "@#@"	
		&& Field does not exist
		RETURN .F.
	ENDIF
	
	IF INLIST(lcJnlType, "CI", "CC")
		&& Invoice or Credit note
		IF !EMPTY(lcEmailAdressForInvoice) AND !EMPTY(lcEmailAdressForOtherDoc) AND PADR(lcEmailAdressForInvoice,250) == PADR(lcEmailAdressForOtherDoc,250)
			&& IF cust.EMAIL and cust.C_EMAILOD are filled with the same adress, this means that the customer no longer wants to receive their invoices and credit notes by email.
			&& In this case, we reset the email address to blank.
			&& We cannot reset the CUST.EMAIL field, because the synchronization with the accounting system will reset the email address in the accounting database.
			pcEmail = ""
		ELSE
			pcEmail = lcEmailAdressForInvoice
		ENDIF
		RETURN .T.
	ELSE
		&& Documents other than invoices and credit notes
		IF !EMPTY(lcEmailAdressForOtherDoc)
			pcEmail = lcEmailAdressForOtherDoc		&& Fill CUST.C_EMAILOD for documents other than invoices and credit notes
		ELSE		
			pcEmail = lcEmailAdressForInvoice		&& If C_EMAILOD is empty, return CUST.EMAIL
		ENDIF		
		RETURN .T.
	ENDIF
	
	RETURN .F.

*---------------------------------------------------------------------------------	
PROCEDURE E_SendMailUpdate
	LPARAMETERS pcTo, pcSubject, pcBody, pcOption, pcAttach1, pcAttach2, pcAttach3, pcAttach4, pcAttach5, pcAttach6, pcAttach7, pcAttach8, pcAttach9

	IF USED("__Cust") AND TYPE("__Cust.C_EMAILOD") = "C" AND LOWER(ALLTRIM(__Cust.Email)) = LOWER(ALLTRIM(pcTo)) AND PADR(LOWER(__Cust.Email),250) == PADR(LOWER(__Cust.C_EMAILOD),250) AND ;
		("FAC " $ UPPER(pcSubject) OR "NCC " $ UPPER(pcSubject))
		&& IF cust.EMAIL and cust.C_EMAILOD are filled with the same adress, this means that the customer no longer wants to receive their invoices and credit notes by email.
		&& In this case, we reset the email address to blank.
		&& We cannot reset the CUST.EMAIL field, because the synchronization with the accounting system will reset the email address in the accounting database.		
		pcTo = ""	
	ENDIF
	
	RETURN
				
*--------------------------------------------------------------------------------
PROCEDURE C_SendToPeppolViaGowam

    LOCAL lcErrorMsg, lcJnl, lcNumberFrom, lcNumberTo, lcStatus, llCanSend

	lcErrorMsg 		= ""
    lcJnl			= ALLTRIM(c_dochead.jnl)
    lcNumberFrom	= TRANSFORM(c_dochead.Number)
    lcNumberTo		= TRANSFORM(c_dochead.Number)
    llCanSend		= .T.
    lcStatus 		= C_GetPeppolTecStatus(lcJnl, VAL(lcNumberFrom))
    
    DO CASE
    	CASE "OK" $ lcStatus AND NOT "not to sent" $ LOWER(lcStatus)
    		Info("This document has already been sent and accepted on the Peppol network.")
    		llCanSend	= .F.
    	CASE "WAIT" $ lcStatus
    		IF NOT YesNoConfirm("This document is currently being processed by Gowam, do you really want try again ?")	
    			llCanSend = .F.
    		ENDIF
    ENDCASE	
    	    
	IF llCanSend
	   	&& Send
    	IF TYPE("goPdf") = "O"
    		IF NOT goPdf.IsDocPdf(c_dochead.jnl, c_dochead.Number)
    			IF NOT YesNoConfirm("No PDF in Logistics for this document, do you want to continue and do a call to Gowam ?")
    				RETURN
    			ENDIF
    		ENDIF
    	ENDIF	
	   	C_SendToPeppol(lcJnl, lcNumberFrom, lcNumberTo, @lcErrorMsg)
	ENDIF
    	
	IF !EMPTY(lcErrorMsg)
		Stop(lcErrorMsg)
	ENDIF
         
ENDPROC

*--------------------------------------------------------------------------------
PROCEDURE C_SendToPeppol
	LPARAMETERS pcJnl, pcNumberFrom, pcNumberTo, pcErrorMsg
	
	C_SendToPeppolLog( MSG("Call C_SendToPeppol start with : pcJnl = %1, pcNumberFrom = %2, pcNumberTo = %3", pcJnl, pcNumberFrom, pcNumberTo) )

	LOCAL lcActualdir, lcGowamPath, lcGowamCustomPath, lcFolder, lcUser, lcJnl, lcNumberFrom, lcNumberTo

	lcActualdir 		= SYS(5)+CURDIR()
	lcFolder 			= ALLTRIM(gcCompId)
	lcUser 				= ALLTRIM(gcUserId)
	lcGowamPath 		= ADDBS(lcActualdir+"Gowam")
	lcGowamCustomPath 	= GetParam("C_GOWAM_INSTALL_PATH", "")	&& For TS session where we have a lot of logistics workstation, set an unique directory where Gowam is installed
	IF !EMPTY(lcGowamCustomPath)
		lcGowamPath = ADDBS(lcGowamCustomPath)
	ENDIF
	lcJnl				= ALLTRIM(pcJnl)
	lcNumberFrom 		= pcNumberFrom
	lcNumberTo			= pcNumberTo
    
	IF !FILE(lcGowamPath+"gowam.exe")
    	pcErrorMsg = MSG("The Gowam program has not been installed (%1) for this Logistics Workstation", lcGowamPath)
    	C_SendToPeppolLog( pcErrorMsg )
    	RETURN
    ENDIF    

    CD (lcGowamPath)
    	
	LOCAL lnOldSelect, loShell, lcCommande, loExec, loError
	
	lnOldSelect = SELECT(0)
	
	TRY

	    * Create the Shell object
	    loShell = CREATEOBJECT("WScript.Shell")
	    
	    lcGowamPath = lcGowamPath+"gowam.exe"
	    
	    * Build the command
	    lcCommande = ["] + lcGowamPath + [" --folder="] + lcFolder + [" --user="] + lcUser + [" --jnl="] + lcJnl + [" --numberFrom="] + lcNumberFrom + [" --numberTo="] + lcNumberTo +["]
	        
	    * Generate the CRM action(s) that indicate that a request has been made to Gowam
	    IF lcNumberFrom = lcNumberTo OR VAL(lcNumberTo) < VAL(lcNumberFrom)
	    	IF NOT "WAIT" $ C_GetPeppolTecStatus(lcJnl, VAL(lcNumberFrom))
		   		C_AddHistPeppolAction(lcJnl, VAL(lcNumberFrom), "WAIT", "Processing in progress by Gowam")
		   	ENDIF
	   	ELSE
	   		LOCAL lnAction, lnTotAction
	   		lnTotAction = (VAL(lcNumberTo)-VAL(lcNumberFrom)) + 1
	   		FOR lnAction = 1 TO lnTotAction
	   			IF NOT "WAIT" $ C_GetPeppolTecStatus(lcJnl, (VAL(lcNumberFrom)-1)+lnAction)
	   				C_AddHistPeppolAction(lcJnl, (VAL(lcNumberFrom)-1)+lnAction, "WAIT", "Processing in progress by Gowam")
	   			ENDIF
	   		ENDFOR	
	   	ENDIF
	   	
	   	C_SendToPeppolLog( "Before Call C_SendToPeppol with : "+TRANSFORM(lcCommande) )

	    * Launch in the background
	    loExec = loShell.Run(lcCommande, 1, .F.)
	    
	    C_SendToPeppolLog( "After Call C_SendToPeppol" )

	CATCH TO loError
		CatchError(loError)
		pcErrorMsg = TRANSFORM(loError.Errorno)+" : "+loError.Message+CHR(13)+"Line : "+TRANSFORM(loError.lineno)+CHR(13)+"Procedure : "+TRANSFORM(loError.Procedure)+ CHR(13)+"Line Contents : "+TRANSFORM(loError.LineContents)
		C_SendToPeppolLog( "Call C_SendToPeppol error catch : "+pcErrorMsg )
	ENDTRY
   
   CD (lcActualdir)     			

*--------------------------------------------------------------------------------
PROCEDURE C_SendToPeppolLog
	LPARAMETERS pcMessage
	
	LOCAL lcGowamPeppolLogDir, lcLogFile 
	
	lcGowamPeppolLogDir = gcDataDir+"GOWAM\LOG\"
	lcLogFile = TRANSFORM(YEAR(DATE()))+"-"+PADL(TRANSFORM(MONTH(DATE())),2,"0")+"-"+PADL(TRANSFORM(DAY(DATE())),2,"0")+".log"
	
	IF !DIRECTORY(lcGowamPeppolLogDir)
		MKDIR (lcGowamPeppolLogDir)
	ENDIF
	
	IF !DIRECTORY(lcGowamPeppolLogDir)
		RETURN
	ENDIF
	
	TRY
		STRTOFILE(TRANSFORM(DATETIME())+"-"+ComputerName()+"-"+gcUserId+":"+pcMessage+CHR(13)+CHR(10), lcGowamPeppolLogDir+lcLogFile, 1)
	CATCH
	ENDTRY
	
	RETURN 
	
	
*///////////////////////////////////////////////////////////////////////////////////////////////////////////
DEFINE CLASS C_RefreshPeppolTimer AS Timer
	
	Interval 			= 200		&& Specifies the number of milliseconds between calls to a Timer control's Timer event
	lTimerInWait		= .F.
	lExit				= .F.
	zoDocForm			= NULL

	*--------------------------------------------------------------------------------
	FUNCTION Init
		LPARAMETERS poDocForm
		
		This.zoDocForm = poDocForm
		
		This.zoDocForm.zoC_PeppolTextBoxStatus.Alignment = 2
		
	
	*--------------------------------------------------------------------------------
	PROCEDURE Timer
	
		IF This.lTimerInWait
			RETURN
		ENDIF
		
		This.lTimerInWait = .T.
		
		IF NOT This.zoDocForm.zoFormMainGroup.DocIsInEdit()
			This.RefreshPeppolEditBox()
		ENDIF
			
		This.lTimerInWait = .F.

	*--------------------------------------------------------------------------------
	PROCEDURE RefreshPeppolEditBox
		
		This.zoDocForm.zoC_PeppolTextBoxStatus.DisabledForeColor = RGB(255,255,255)
	
		DO CASE
			CASE "OK" $ This.zoDocForm.zoC_PeppolTextBoxStatus.Value
				This.zoDocForm.zoC_PeppolTextBoxStatus.DisabledBackColor = RGB(76,175,80)	&& Vert de Gowam	
			CASE "KO" $ This.zoDocForm.zoC_PeppolTextBoxStatus.Value
				This.zoDocForm.zoC_PeppolTextBoxStatus.DisabledBackColor = RGB(244,67,54)	&& Rouge de Gowam
			CASE "TODO" $ This.zoDocForm.zoC_PeppolTextBoxStatus.Value
				This.zoDocForm.zoC_PeppolTextBoxStatus.DisabledBackColor = RGB(150,243,1)	&& Bleu de Gowam	
			CASE "WAIT" $ This.zoDocForm.zoC_PeppolTextBoxStatus.Value
				This.zoDocForm.zoC_PeppolTextBoxStatus.DisabledBackColor = RGB(255,152,0)	&& Orange de Gowam	
			OTHERWISE
				This.zoDocForm.zoC_PeppolTextBoxStatus.DisabledBackColor = This.zoDocForm.BackColor
		ENDCASE
		This.zoDocForm.zoC_PeppolTextBoxStatus.Refresh()
		This.zoDocForm.zoC_PeppolEditBox.Refresh()
	
	*--------------------------------------------------------------------------------
	FUNCTION Destroy
	
		
ENDDEFINE
*///////////////////////////////////////////////////////////////////////////////////////////////////////////	
	
